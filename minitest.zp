(module "minitest"
 (export
  (list :assert-equal assert-equal)
  (list :assert-not-equal assert-not-equal)
  (list :assert-true assert-true)
  (list :assert-false assert-false)
  (list :assert-truthy assert-truthy)
  (list :assert-falsy assert-falsy)
  (list :assert-nil assert-nil)
  (list :assert-not-nil assert-not-nil)
  (list :all-passed all-passed)
  (list :exit-first-fail exit-first-fail)
  (list :colorize colorize)
  (list :verbose verbose)
  (list :results results))

  (pass-count 0)
  (fail-count 0)

  (_exit #f)
  (_colorize #f)
  (_verbose #f)
  (_c (lambda (x) (if _colorize (color x))))
  (exit-first-fail (lambda (x)
    "sets whether the tests should terminate after the first failing test.
     (defaults: false)

     params:
      - x: the truth value
     complexity: O(1)
     returns: <par>x</par>"
    (set! _exit x)))
  (colorize (lambda (x)
    "sets whether the output should be colorized.
     (defaults: false)

     params:
      - x: the truth value
     complexity: O(1)
     returns: <par>x</par>"
    (set! _colorize x)))
  (verbose (lambda (x)
    "sets whether the output should be verbose.
     (defaults: false)

     params:
      - x: the truth value
     complexity: O(1)
     returns: <par>x</par>"
    (set! _verbose x)))

  (unit-test-handler (lambda (expected actual descr what . eq) 
    (let ((test (get-from eq 0 (lambda (x y) (eqv? x y)))))
      (if (not (test expected actual))
        (begin (if _verbose 
               (begin (_c :red) (map display (reverse (list "Test \"" descr "\" failed\n\t"))) (_c :reset)
               (map display (reverse (list "Expected " what ": \"" expected "\", actual value: \"" actual "\"\n"))))
                (begin (_c :red) (display "F") (_c :reset)))
               (if _exit (begin (_c :red) (write "Exiting on first fail") (_c :reset) (exit 1)))
               (set! fail-count (+ fail-count 1)))
        (begin (if _verbose
                (begin (_c :green) (write (++ "Test \"" descr "\" passed")) (_c :reset))
                (begin (_c :green) (display "P") (_c :reset)))
        (set! pass-count (+ pass-count 1)))))))

  (assert-equal (lambda (x y . descr)
    "assert that <par>x</par> and <par>y</par> are equal.

     params:
      - x: the expected value
      - y: the actual value
      - descr: optional argument that describes the test
     complexity: O(1)
     returns: the number of passed tests"
    (let ((descr (get-from descr 0 "assert-equal")))
      (unit-test-handler x y descr "value"))))
  (assert-not-equal (lambda (x y . descr)
    "assert that <par>x</par> and <par>y</par> are not equal.

     params:
      - x: the expected value
      - y: the actual value
      - descr: optional argument that describes the test
     complexity: O(1)
     returns: the number of passed tests"
    (let ((descr (get-from descr 0 "assert-not-equal")))
      (unit-test-handler x y descr "not" (lambda (x y) (not (eqv? x y)))))))
  (assert-true (lambda (x . descr)
    "assert that <par>x</par> is true.

     params:
      - x: the given value
      - descr: optional argument that describes the test
     complexity: O(1)
     returns: the number of passed tests"
    (let ((descr (get-from descr 0 "assert-true")))
      (assert-equal #t x descr))))
  (assert-false (lambda (x . descr)
    "assert that <par>x</par> is false.

     params:
      - x: the given value
      - descr: optional argument that describes the test
     complexity: O(1)
     returns: the number of passed tests"
    (let ((descr (get-from descr 0 "assert-false")))
      (assert-equal #f x descr))))
  (assert-truthy (lambda (x . descr)
    "assert that <par>x</par> is truthy.

     params:
      - x: the given value
      - descr: optional argument that describes the test
     complexity: O(1)
     returns: the number of passed tests"
    (let ((descr (get-from descr 0 "assert-truthy")))
      (unit-test-handler x (truthy? x) descr "to be truthy" (lambda (x y) (truthy? x))))))
  (assert-falsy (lambda (x . descr)
    "assert that <par>x</par> is falsy.

     params:
      - x: the given value
      - descr: optional argument that describes the test
     complexity: O(1)
     returns: the number of passed tests"
    (let ((descr (get-from descr 0 "assert-falsy")))
    (unit-test-handler x (falsy? x) descr "to be falsy" (lambda (x y) (falsy? x))))))
  (assert-nil (lambda (x . descr)
    "assert that <par>x</par> is nil.

     params:
      - x: the given value
      - descr: optional argument that described the test
     complexity: O(1)
     returns: the number of passed tests"
    (let ((descr (get-from descr 0 "assert-nil")))
      (assert-equal (nil) x descr))))
  (assert-not-nil (lambda (x . descr)
    "assert that <par>x</par> is not nil.

     params:
      - x: the given value
      - descr: optional argument that described the test
     complexity: O(1)
     returns: the number of passed tests"
    (let ((descr (get-from descr 0 "assert-not-nil")))
      (assert-not-equal (nil) x descr))))

  (results (lambda ()
    "prints the test results.

     complexity: O(1)
     returns: the number of failed tests"
    (begin
      (write "Results: ")
      (if _verbose (begin
        (_c :green) 
        (if (> pass-count 0) (display (++ "\t|" (string:replicate pass-count "="))))
        (if (eq? fail-count 0) (write "|"))
        (_c :red)
        (if (eq? pass-count 0) (display "\t|"))
        (if (> fail-count 0) (write (++ (string:replicate fail-count "=") "|")))))
      (_c :green) (map display (reverse (list "\tPassed: " pass-count)))
      (_c :red) (map display (reverse (list " Failed: " fail-count "\n"))) (_c :reset)
      fail-count)))
  (all-passed (lambda ()
    "returns whether the tests all passed.

     complexity: O(1)
     returns: a boolean"
    (= fail-count 0))))
